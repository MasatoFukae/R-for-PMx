;; 1. Based on: run56
;; 2. Description: Final model
;; x1. Author: user
;; Including all data along with rich datset
;; 3. Label:
;;
;; 4. Structural model:
;; 2 CMT, IV administration
;; 5. Covariate model:
;; WT on V1 (both), WT on CL (IP), CRCL on CL (both) & HLTH on V1 (IP)
;; 6. Interindividual variability:
;; IIV on CL & V1 & V2 (Both analytes), covariance on CL & V1 (Both analytes)
;; 7. Interoccasion variability:
;; None
;; 8. Residual variability:
;; Proportional (both analytes)
;; 9. Estimation:
;; FO + I
;;
$PROBLEM MK-7655A Population PK Analysis
;;
;; Sponsor: Merck
;; Project: MK-7655A
;; Analyst: Kenny Watson / Parul Patel
;;
;; ------------------------------------------------------------------------------------------------------------------
$INPUT C NROW PN PID ID PART PANL PERD GRP SDMD COAD DRUG DOSE
 DAY RNTM TIME TSLD DV MDV EVID AMT RATE ADDL II CMT=DROP
 CRCL RENL WT WTBN AGE MALE HLTH HT RACE SECR RICH INFC TRT
; AMT = nmol; DV = nmol/L; TIME = h
; CL and Q = L/hr; V1 & V2 = L
$DATA PSP4-8-748-s012.csv IGNORE=@
$SUBROUTINE ADVAN3 TRANS4
;
$PK
;;; V1_RLWT-DEFINITION START
IF(WT.EQ.-99) THEN
 V1_RLWT = 1
ELSE
 V1_RLWT = ((WT/76)**THETA(14))
ENDIF
;;; V1_RLWT-DEFINITION END

;;; V1_RL-RELATION START
V1_RLCOV=V1_RLWT
;;; V1_RL-RELATION END

;;; V1_IPWT-DEFINITION START
IF(WT.EQ.-99) THEN
 V1_IPWT = 1
ELSE
 V1_IPWT = ((WT/76)**THETA(13))
ENDIF
;;; V1_IPWT-DEFINITION END

;;; V1_IPHLTH-DEFINITION START
IF(HLTH.EQ.0) V1_IPHLTH = 1 ; Most common
IF(HLTH.EQ.1) V1_IPHLTH = ( 1 + THETA(12))
;;; V1_IPHLTH-DEFINITION END

;;; V1_IP-RELATION START
V1_IPCOV=V1_IPHLTH*V1_IPWT
;;; V1_IP-RELATION END

;;; CL_RLCRCL-DEFINITION START
IF(CRCL.EQ.-99) THEN
 CL_RLCRCL = 1
ELSE
 CL_RLCRCL = ((CRCL/109)**THETA(11))
ENDIF
;;; CL_RLCRCL-DEFINITION END

;;; CL_RL-RELATION START
CL_RLCOV=CL_RLCRCL
;;; CL_RL-RELATION END

;;; CL_IPWT-DEFINITION START
IF(WT.EQ.-99) THEN
 CL_IPWT = 1
ELSE
 CL_IPWT = ((WT/76)**THETA(10))
ENDIF
;;; CL_IPWT-DEFINITION END

;;; CL_IPCRCL-DEFINITION START
IF(CRCL.EQ.-99) THEN
 CL_IPCRCL = 1
ELSE
 CL_IPCRCL = ((CRCL/109)**THETA(9))
ENDIF
;;; CL_IPCRCL-DEFINITION END

;;; CL_IP-RELATION START
CL_IPCOV=CL_IPCRCL*CL_IPWT
;;; CL_IP-RELATION END

; Imipenem PK parameters
 TVCL_IP = THETA(1)
TVCL_IP = CL_IPCOV*TVCL_IP
 TVV1_IP = THETA(2)
TVV1_IP = V1_IPCOV*TVV1_IP
 TVV2_IP = THETA(3)
 TVQ_IP = THETA(4)
 CL_IP = TVCL_IP*EXP(ETA(1))
 V1_IP = TVV1_IP*EXP(ETA(2))
 V2_IP = TVV2_IP*EXP(ETA(3))
 Q_IP = TVQ_IP*EXP(ETA(4))

; MK-7655 parameters
 TVCL_RL = THETA(5)
TVCL_RL = CL_RLCOV*TVCL_RL
 TVV1_RL = THETA(6)
TVV1_RL = V1_RLCOV*TVV1_RL
 TVV2_RL = THETA(7)
 TVQ_RL = THETA(8)
 CL_RL = TVCL_RL*EXP(ETA(5))
 V1_RL = TVV1_RL*EXP(ETA(6))
 V2_RL = TVV2_RL*EXP(ETA(7))
 Q_RL = TVQ_RL*EXP(ETA(8))
 IF(DRUG.EQ.1) THEN
 CL = CL_IP
 V1 = V1_IP
 V2 = V2_IP
 Q = Q_IP
 S1 = V1_IP
 ELSE
 CL = CL_RL
 V1 = V1_RL
 V2 = V2_RL
 Q = Q_RL
 S1 = V1_RL
 ENDIF
;
$ERROR
;
 DEL=0
 IF (F.LE.0.0001) DEL=1
 IPRED=F
 W1= 1
 W2= F
 IRES= DV-IPRED
 IWRES=(1-DEL)*IRES/(W1+W2+DEL)
;
 X1=0
 X2=0
;
IF (DRUG.EQ.1) X1=1
IF (DRUG.EQ.2) X2=1
RV_IP = X1*(F*(1+ERR(1)) + ERR(2))
RV_MK = X2*(F*(1+ERR(3)) + ERR(4))
Y = RV_IP + RV_MK
;
;
$THETA (0,11.7) ; [TVCL_IP] (L/h)
$THETA (0,14.5) ; [TVV1_IP] (L)
$THETA (0,6.3) ; [TVV2_IP] (L)
$THETA (0,11.1) ; [TVQ_IP] (L/h)
$THETA (0,6.52) ; [TVCL_RL] (L/h)
$THETA (0,11.1) ; [TVV1_RL] (L)
$THETA (0,6.65) ; [TVV2_RL] (L)
$THETA (0,10.3) ; [TVQ_RL] (L/h)
$THETA (0,0.5, 2.00) ; CL_IPCRCL1
$THETA (0,1, 2.00) ; CL_IPWT1
$THETA (0,0.5, 2.00) ; CL_RLCRCL1
$THETA (-1,-0.001,5) ; V1_IPHLTH1
$THETA (0,1, 2.00) ; V1_IPWT1
$THETA (0,1, 2.00) ; V1_RLWT1

$OMEGA BLOCK(2)
0.306 ;[BSV_CL_IP]
0.103 ;[CORR_CL_V1_IP]
0.599 ;[BSV_V1_IP]

$OMEGA
 0.126 ; [BSV_V2_IP]
 0 FIX ; [BSV_Q_IP]

$OMEGA BLOCK(2)
0.351 ;[BSV_CL_RL]
0.103 ;[CORR_CL_V1_RL]
0.328 ;[BSV_V1_RL]

$OMEGA
 0.168 ; [BSV_V2_RL]
 0 FIX ; [BSV_Q_RL]

$SIGMA 0.025 ; [RES_prop_IP]
$SIGMA 0 FIX ; [RES_add_IP]
$SIGMA 0.0223 ; [RES_prop_RL]
$SIGMA 0 FIX ; [RES_add_RL]
$ESTIMATION METHOD=1 INTER MAXEVAL=9999 PRINT=5 SIG=3 MSFO=55.MSF
;
$COVARIANCE PRINT=E
$TABLE PN PID ID PART PANL PERD GRP SDMD COAD DRUG DOSE DAY RNTM TIME TSLD
CRCL RENL WT WTBN AGE MALE HLTH HT RACE SECR RICH NROW CWRES TRT ONEHEADER NOPRINT FORMAT=s1PE13.7 FILE=mytab60
;
$TABLE ID NROW PN DRUG DOSE DAY RNTM TIME TSLD IPRED IRES IWRES CWRES DV ONEHEADER NOPRINT FORMAT=s1PE13.7 FILE=sdtab60
$TABLE ID CL_IP V1_IP Q_IP V2_IP CL_RL V1_RL Q_RL V2_RL
ETA1 ETA2 ETA3 ETA4 ETA5 ETA6 ETA7 ETA8 NOAPPEND ONEHEADER NOPRINT FORMAT=s1PE13.7 FILE=patab60
$TABLE ID MALE HLTH RENL WTBN RACE ONEHEADER NOPRINT NOAPPEND FORMAT=s1PE13.7 FILE=catab60
$TABLE ID CRCL WT AGE HT SECR NOAPPEND ONEHEADER NOPRINT FORMAT=s1PE13.7 FILE=cotab60

;